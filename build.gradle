plugins {
    id 'java-library'
    id 'maven-publish'
}

version = '2.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.8.9'
    implementation 'org.slf4j:slf4j-api:1.7.33'
    implementation 'org.apache.httpcomponents:httpcore:4.4.15'
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/main/java',
                    "$buildDir/generated/java",
            ]
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    template {
        java {
            srcDirs = ['src/template/java']
        }
    }
}

task generateSources(type: Copy) {
    def vars = ['version': project.version]
    inputs.properties vars
    from('src/template/java')
    into "$buildDir/generated/java"
    expand vars
}

compileJava.dependsOn generateSources

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.lion328.namtium'
            artifactId = 'namtium'

            from components.java
        }
    }
}
