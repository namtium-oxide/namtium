plugins {
    id 'java-library'
}

version = '2.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.8.9'
    implementation 'org.apache.logging.log4j:log4j-core:2.12.4'
    implementation 'org.apache.httpcomponents:httpcore:4.4.15'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.lion328.namtium.Main'
    }

    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it);
        }
    }

    duplicatesStrategy = DuplicatesStrategy.WARN

    exclude [
        'META-INF/*.SF',
        'META-INF/*.DSA',
        'META-INF/*.RSA',
        'META-INF/LICENSE',
        'META-INF/NOTICE',
        'META-INF/DEPENDENCIES',
    ]
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/main/java',
                    "$buildDir/generated/java",
            ]
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

task generateSources(type: Copy) {
    def vars = ['version': project.version]
    inputs.properties vars
    from('src/template/java')
    into "$buildDir/generated/java"
    expand vars
}

compileJava.dependsOn generateSources
